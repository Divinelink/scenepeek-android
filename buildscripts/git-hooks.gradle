static def isLinuxOrMacOs() {
  def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
  return osName.contains('linux') || osName.contains('mac os') || osName.contains('macos')
}

task copyGitHooks(type: Copy) {
  description "Copies the git hooks from ${rootDir}/git-hooks/ to the .git folder."
  from("${rootDir}/git-hooks/") {
    include '**/*.sh'
    rename '(.*).sh', '$1'
  }
  into "${rootDir}/.git/hooks"
  onlyIf { isLinuxOrMacOs() }
  doLast {
    println('Copy successful.')
  }
}

task installGitHooks(type: Exec) {
  description "Installs the pre-commit git hooks from ${rootDir}/git-hooks/"
  group 'git-hooks'
  workingDir rootDir
  commandLine 'chmod'
  args '-R', '+x', '.git/hooks/'
  dependsOn copyGitHooks
  onlyIf { isLinuxOrMacOs() }
  doLast {
    println('Git hook installed successfully.')
  }
}

afterEvaluate {
  // We install the hook at the first occasion
  tasks['clean'].dependsOn installGitHooks
  tasks['assemble'].dependsOn installGitHooks
  android.applicationVariants.all { v ->
    if (v.name.endsWith('Debug')) {
      String name = v.name.capitalize()
      tasks["assemble$name"].dependsOn installGitHooks
    }
  }
}